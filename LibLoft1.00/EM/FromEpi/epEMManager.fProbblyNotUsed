! #include "ZsubstRec.h"
!     #include "Zepcondc.h"
      module modintManager
      character(8):: intProcess
      integer:: MagPair=0
      end module modintManager 
      module modEMcontrol
      real(8):: Xai
      real(8):: Bfield(3)
      end  module modEMcontrol

      
      subroutine epqProc(proc)
      use modintManager
!          inquire the current process fixed
      implicit none
!#include  "ZepManager.h"
!     #include  "ZepTrackv.h"
      character(8),intent(out)::proc
!     proc = Move%proc   --->make  Pocess in  ZepManager
      proc = intProcess
      end

      subroutine epprog(gProj, media)
!       use modXsecMedia, xmedia=>media, xelement=>element,
!     *     dontuse=>NoOfMedia
       implicit none
!     6 processes:
!     pair creation, compton, Photo-electric effect, coherent scatt.
!     magnetic pair creation, and Photo-production of hadrons are considered.
!     
#include  "Zmedia.h"
#include  "Zptcl.h"       
#include  "Zevhnv.h"
       
#include  "ZepTrackv.h"
#include  "ZepTrackp.h"


!
      type(ptcl),intent(in):: gProj
      type(epmedia),intent(in):: media
       
      real*8 tcomp, tphot, tpair, tgp, tcoh,  t
      real*8 E, prob, xprob(5), txray(5)
      real*8 xs, mfp
!     real*8  pairmfp, dl, dx, tmpair, u
      real*8  pairmfp, dl, dx, lmpair, u   ! lmpair in m
      real*4  xsec(5)  !  coh, incoh,  P.E  1/(g/cm2)
!      real*8  Excom1, Excom2  !now in ZepTrackp.h
      integer icon
!            where xcom data is used. 
!       Excom1: compton/photo abs/coherent scat
!       Excom2: pair; default is  not use xcom
!              both must be < 100 GeV
!      data Excom1/1.d-3/, Excom2/1.d-3/
!      save Excom1, Excom2

      E = gProj%p%fm%p(4)
      if(E .le. EupperBndCS) then
         xprob(3)= 0.
         if( media%xcom%size .gt. 0 .and.
     *        E .lt. Excom1 ) then
!               below 1MeV, use accurate xs.
            call epXrayp(media, E, 1,  3,  xprob, txray)
            tcomp=txray(2)
            tcoh = txray(1)
            tphot = txray(3)
         else
            tcoh = 1.e35
            call epcompp(media, E, prob, tcomp)
         endif
         if(Photo) then
            if(xprob(3)  .eq. 0.) then
               call epphotoEp(media,  E, prob, tphot) ! v8.0
            endif
         else
            tphot = 1.e35
         endif
      else
         tcomp =1.e35
         tphot= 1.e35
         tcoh = 1.e35
      endif
      
      if(E .gt. ElowerBndPair) then
         if( media%xcom%size .gt. 0 .and.
     *        E .lt. Excom2 ) then
            call epXrayp(media, E, 4, 5,  xprob, txray)
            prob= xprob(4)+xprob(5)
            call rndc(u)
            tpair = -log(u)/prob
         else
            call epPrSampP(media, E, prob, tpair)
         endif
      else
         tpair=1.e35
      endif
      if(IncGp > 0 .and. E .gt. 153.d-3) then ! > 0.152431798977028
         call ep2cosCond
         call cfixModel( gProj )
         call cgetPhotoPxs(ActiveMdl2, gProj, media,  xs, mfp)
         prob = xs*media%mbtoPX0 ! prob/r.l
         call rndc(u)
         tgp = -log(u)/prob          ! sampled path in r.l
      else
         tgp=1.e35
      endif
!!!!!!!!!!!!!!!!!!!!
      if(MagPair .eq. 1) then
!     call epmpairp(gProj, Bfield, Xai, pairmfp, dl)
         call epmpairp(gProj, Bfield, Xai, pairmfp, lmpair)
!          conversion business  is left to each application.
!         dx = dl / media%gtocm * media%rhoc
!         tmpair = dx / media%X0g
      else
!     tmpair = 1.e35
         lmpair = 1.e35
      endif
!!!!!!!      
      t=min(tpair, tcomp, tphot, tgp, tmpair, tcoh)
      if(t .eq. tpair) then
!     Move%proc='pair'
         intProcess = 'pair'
      elseif(t .eq. tcomp) then
!     Move%proc='comp'
         intProcess='comp'
      elseif(t .eq. tphot) then
!     Move%proc='phot'
         intProcess ='phot'
      elseif(t .eq. tcoh) then
!     Move%proc='coh'
         intProcess = 'coh'
      elseif(t .eq. tgp) then
         intProcess ='photop'
      else
         intProcess ='mpair'
      endif
!!      Move%dt = t   ! in r.l
!!      Move%dx = Move%dt * Media(MediaNo)%X0g
!!      Move%dl = Move%dx * Media(MediaNo)%gtocm /
!!     *     Media(MediaNo)%rhoc
      end
!     *************
      subroutine  epproe
!     *************
!!!!!!!!!
      use moddebug
!!!!!!!!!      
      use modMCScontrol
      implicit none
!
!   electron:     brems, knock-on, and anihilation, synchrotron
!                 radiation are considered
!
#include  "ZepTrackv.h"
#include  "ZepTrackp.h"
!
      real*8 E, prob,  tbrem, tknock, tanihi, t, dt, dl, dx,
     *   syncmfp
      real(8)::u
      logical:: posok, numok
      type(epPos):: posw
      integer:: i
       
      E = cTrack%p%fm%p(4)
!             sample path for brems
      call epBrSampP(Media(MediaNo),  E, prob, tbrem)

      if(Knckon) then
         if(cTrack%p%charge .eq. -1) then
            call epmollerp(Media(MediaNo), E, RecoilKEmin, prob, tknock)
         else
            call epbhabhap(Media(MediaNo), E, RecoilKEmin, prob, tknock)
         endif
         if(tbrem .le. tknock) then
            t = tbrem
            Move%proc='brem'
         else
            t = tknock
            Move%proc='knoc'
         endif
      else
         t = tbrem
         Move%proc='brem'
      endif
      if(cTrack%p%charge .eq. 1 .and. E .lt. Eanihi) then
         call epanihip(Media(MediaNo), E, prob, tanihi)
         if(tanihi .lt. t) then
            t = tanihi
            Move%proc='anih'
         endif
      endif

      if( doNewMCS ) then
         if( (Media(MediaNo)%name /= 'sp' ) .and.
     *       (Media(MediaNo)%name /= 'hollow')  ) then
            call cfixMCSmodel( cTrack%p ) ! see energetically OK ?

            if( ActiveMCS /= 'Mol') then

               if( MCSzCond > 0 ) then
!                see if z is in the specifed region; else -> Mol
                  posok = .false.
                  call epl2w(cTrack%cn, cTrack%pos, posw)
                  do i = 1, MCSzCond
                     if(real(MCSzRange(i)) <= posw%z ) then
                        if( posw%z <= imag(MCSzRange(i)) ) then
                           posok = .true.
                           exit
                        endif
                     endif
                  enddo
               else
                  ! no check about Z
                  posok=.true.
               endif

               if( MCSnumCond > 0 ) then
                  if( MCSdebug ) then
                     call epl2w(cTrack%cn, cTrack%pos, posw)
                  endif
                  ! see if cn is in the ranage
                  numok = .false.
                  do i = 1, MCSnumCond
                     if(MCSnumRangeMin(i) <=  cTrack%cn ) then
                        if( cTrack%cn <= MCSnumRangeMax(i) ) then
                           numok = .true.
                           exit
                        endif
                     endif
                  enddo
               else
                  ! no check about cn
                  numok = .true.
               endif


               if( MCSandor == 'and' ) then
                  if( posok .and. numok ) then
                     if( MCSrevert ) then
                        ActiveMCS = 'Mol'
                     else
                        !  keep current one
                     endif
                  else
                     if( .not. MCSrevert ) then
                        ActiveMCS = 'Mol'
                     else
!                      keep current
                     endif
                  endif
                  
               else             ! 'or'
                  if( posok .or.  numok ) then
                     if(  MCSrevert ) then
                        ActiveMCS = 'Mol'
                     else
!                         keep current
                     endif
                  else
                     if( .not. MCSrevert ) then
                        ActiveMCS = 'Mol'
                     else
!                          keep current
                     endif
                  endif
               endif
               if( MCSdebug ) then 
                  if( ActiveMCS == 'Mol') then
                     write(0,'(a, 1p,3g14.4)') 'mol: ',  posw
                  else
                     write(0,'(a, 1p,3g14.4)') 'hin: ',  posw
                  endif
               endif
            else
               ActiveMCS = 'Mol'
            endif
         else
            ActiveMCS = 'Mol'
         endif
      endif
      
      if( ActiveMCS /= 'Mol' ) then
         call cfixMixedConst(MediaNo, int(cTrack%p%charge))
      endif


      if( ActiveMCS == 'El_hin') then
             ! sample hard scattering  mfp g/cm2.
         call cgetLamh( KEeV, mfpHardgr)  ! mfp of hard cs
!  in g/cm2
         call rndc(u)
         lHardgr = -log(u)* mfpHardgr  ! g/cm2
         lHardrl =lHardgr / Media(MediaNo)%X0g    ! rl
          !  r.l
          ! next is  cm length. this might  be used later for soft
          ! mcs treatment.
         lHardcm = lHardgr* Media(MediaNo)%gtocm
     *        / Media(MediaNo)%rhoc
         if( lHardrl < t ) then
            Move%proc="hcs"  ! hard Coulomb scattring
            t = lHardrl
         endif
      endif

      Move%dt = t
      Move%dx = Move%dt * Media(MediaNo)%X0g
      Move%dl = Move%dx * Media(MediaNo)%gtocm /
     *    Media(MediaNo)%rhoc

!                     only if X0 > 10 km; may be 30 km is o.k
      if(Sync .eq.  2 .and.
     *    Media(MediaNo)%X0/Media(MediaNo)%rhoc .gt. 10.d5 ) then
!          sample synchrotron emission path
         call epsyncp(cTrack%p, Bfield, Upsilon, syncmfp, dl)
         dx = dl / Media(MediaNo)%gtocm *
     *    Media(MediaNo)%rhoc
         dt = dx / Media(MediaNo)%X0g
         if(dt .lt.  t) then
            Move%dt = dt
            Move%dx = dx
            Move%dl = dl
            Move%proc = 'sync'
         endif
!      else
      endif
      end
       subroutine epint(icon)
       use modIntInfo
       use modXsecMedia, xmedia=>media, xelement=>element,
     *     dontuse=>NoOfMedia
       use modV1ry
       implicit none
#include  "ZepTrackv.h"
#include  "ZepTrackp.h"
#include  "Zcode.h"
#include  "Zevhnv.h"
      integer icon  ! output.  always 1

      character*100 msg
      integer k,  ia, iz
      real*8 xs
      integer inela
       type(epPos):: temppos

!
!/////////////
!      logical show
!      common /showshow/show
!///////////////

      k = cTrack%p%code
!         almost dummy setting 
!      ia =Media(MediaNo).A
!      iz =Media(MediaNo).Z

#if defined (INTINFO)
      kintInfo = min(k, maxcodeForInt)
      if( codeAforInt(kintInfo) == 0 ) then
          !  we have to inform int info to epUI
          !  get current stack pos
         call epqstn(IntInfo1) ! product is put from posIntInfo1+1
         IntInfo1 = IntInfo1 + 1
      endif            
#endif


      if(k .eq. kphoton) then
         if(Move%proc .eq. 'comp') then
            call epcmpt
         elseif(Move%proc .eq. 'pair') then
            call eppair
         elseif(Move%proc .eq. 'phot') then
            call epphot
         elseif(Move%proc .eq. 'coh') then
            call epcoher
         elseif(Move%proc .eq. 'photop') then
!
            call ep2cosPtcl( cTrack%p )
            call cfixTarget(xmedia(mediumNo)) ! 
            call epcpTargetInfo  ! cp target info 
            call ep2cosCond2  !  
            call cphotop        ! Cosmos function
            call eppushPtcl(cTrack)  ! use pos. info from this ptcl
         elseif(Move%proc .eq. 'mpair') then
            call epmpair
         else
            write(msg,
     *       '('' proccess='',a4,'' for gamma undefined'')')
     *       Move%proc
            call cerrorMsg(msg,0)
         endif
      elseif(k .eq. kelec) then

         if(Move%proc .eq. 'brem') then
            call epbrem
         elseif(Move%proc .eq. 'knoc') then
            call epknoc
         elseif(Move%proc .eq. 'hcs' ) then
!           it has been done in  epdoMixedMCS2; 
!            so simply puth current track in stack
            call eppush(cTrack)
         elseif(Move%proc .eq. 'anih') then
            call epanih
         elseif(Move%proc .eq. 'sync') then
            call epsync
         else
            write(msg,
     *         '('' process='',a4, '' for e is undef.'')') Move%proc
            call cerrorMsg(msg, 0)
         endif
       !>>>>>>>>>>>>>>>>>>>>>>light
      elseif( k == klight) then
         if(Move%proc == "pe" ) then
                  ! photo electron generation at sensor
            call epLightAtSensor
         elseif( Move%proc == "rayl" ) then
                  ! Rayleigh scattering; use Xray region fomulat since
                  ! (1+cos^2)dcos
            call epcoher
         elseif( Move%proc == "absorb" ) then
                  ! absorbed. nothing to do;  not push any thing
         elseif( Move%proc == "wls" ) then
                   ! wave length shift
            call epLightPreWLS
         else
            write(0,*) ' light interacion=', Move%proc
            write(0,*) ' not defined '
            stop
         endif
        !<<<<<<<<<<<<<<<<<<<<<<
      else

         if(Move%proc .eq. 'knoc') then

            call epNEPknoc

         elseif(cTrack%p%code .eq. kmuon ) then
            if( Move%proc == 'decay') then
               call ep2cosPtcl( cTrack%p ) ! set
               call ep2coscond2            ! Move.Track
               call cinteMuon  ! capture is also treated
                           !   don't use nuc. interaction there
            elseif(Move%proc .eq.'pair') then
               call epmuInte
            elseif(Move%proc .eq.'brem') then
               call epmuInte
            elseif(Move%proc .eq.'nuci') then
               call epmuInte
            endif
         else  
            ! hadronic ptcls
            call ep2cosPtcl( cTrack%p )
!            if(  k == kmuon .and. cTrack.p.charge == -1  .and.
!     *           cTrack.p.fm.p(4) <= cTrack.p.mass*1.001) then
!                        1.001 must be the same as in cinteMuon
!                 for decay of stopping mu-, 
!                 we must fix target for capture case; really captured
!                 or decay  is determined in cinteMuon; 

            call cfixTarget(xmedia(mediumNo))
            call epcpTargetInfo ! cp target info 
            call ep2cosCond2

            call cinteNEP       ! cosmos eppp
            call eppushPtcl( cTrack )
         endif
      endif
!     ******************
      subroutine epcmpt
       implicit none
#include  "ZepTrackv.h"
#include  "Zcode.h"

!
       type(epTrack)::  electron
       type(epDirec)::  w

       real*8 e1, eg, tmp, cosg, cose
       real*8 sine, cs, sn, sing

!             sample energies of compton elec. and gamma
       call epcompea(cTrack%p%fm%p(4), eg, e1, cosg, cose)

       tmp=1.d0-cose*cose
       if(tmp .lt. 0.d0) then
          tmp=0.d0
          cose=-1.d0
       endif
       sine = sqrt(tmp)
       call kcossn(cs,sn)

       electron = cTrack        ! copy everything from cTrack 

       electron%w%x = cs*sine
       electron%w%y = sn*sine
       electron%w%z = cose
!            w  get new direc-cos
       call eptransVect(cTrack%w,  electron%w,  electron%w)

       call cmkptc(kelec, regptcl, -1, electron%p)
       electron%p%fm%p(4) = e1
       call epe2p(electron)
!
!                treat gamma as counterpart of electron (negative d.c)
!
       tmp=1.d0-cosg*cosg
       if(tmp .lt. 0.) then
          cosg=-1.
          tmp=0.
       endif
       sing=sqrt(tmp)
       w%x = -cs*sing
       w%y = -sn*sing
       w%z = cosg
       call eptransVect(cTrack%w,  w, cTrack%w)
       cTrack%p%fm%p(4) = eg
       call epe2p(cTrack)
!            since gamma is likely to have large energy, save first

       call eppush(cTrack)
       call eppush(electron)

       end
!
!     ************
      subroutine eppair
!     ************
       implicit none
#include  "ZepTrackv.h"
#include  "Zcode.h"
#include  "Zmass.h"

!
       type(epTrack)::  elec1, elec2


       real*8  e1,  e2,  cos1, cos2
!       real*8 cs, sn,  u,  teta1, teta2
       real*8 cs, sn,  u,  teta2
       real*8 sin1, sin2
       
       integer ic
       
       real*8 Eg
!     
       Eg = cTrack%p%fm%p(4)
!           sample higher energy of pair
       call epPrSampE(Media(MediaNo),  Eg, e1)
!            assign charge
       call rndc(u)
       if(u .lt. .5) then
          ic=-1
       else
          ic=1
       endif
!            the other electron energy
       e2 = Eg - e1
!         sample angle; smaller enery electron must be put
!         last
       call epPairAng(e2, masele, teta2) ! teta2 < pi/2

       if(teta2 .lt. 0.03d0) then
          cos2 = 1. - teta2**2/2
          sin2 = teta2
       else
          cos2 = cos(teta2)
          sin2 = sin(teta2)
       endif
!
       sin1 = sin2 * sqrt(  (e2**2-masele**2)/(e1**2-masele**2) )
       if(sin1 .lt. 0.03d0) then
          cos1 = 1.- sin1**2/2
       else
          cos1 = sqrt(1.d0 - sin1**2)
       endif

!
!          the next simplified treatment is also no problem.
!       teta1=teta2 * e2/e1
!       if(teta1 .lt. 0.03d0) then
!          cos1 = 1. - teta1**2/2
!          sin1 = teta1
!       else
!          cos1 = cos(teta1)
!          sin1 = sin(teta1)
!       endif

       elec1 = cTrack           ! copy everything first
!               sample direction cos. of 1st

       call kcossn(cs,sn)
       elec1%w%x = cs*sin1
       elec1%w%y = sn*sin1
       elec1%w%z = cos1
!           
       call eptransVect(cTrack%w, elec1%w, elec1%w)

       call cmkptc(kelec, regptcl, ic, elec1%p)
       elec1%p%fm%p(4) = e1
       call epe2p(elec1)
!              push higher energy none
       call eppush(elec1)
!               lower energy electron

       elec2 = elec1
       elec2%p%fm%p(4) = e2
       call cmkptc(kelec, antip, -ic, elec2%p)

!           treat the other one as counter part    (negative d.c)
       elec2%w%x = -cs*sin2
       elec2%w%y = -sn*sin2
       elec2%w%z = cos2
       call eptransVect(cTrack%w, elec2%w, elec2%w)
       call epe2p(elec2)
       call eppush(elec2)
       end
!      ************
       subroutine epphot
!      ************
       implicit none
#include  "ZepTrackv.h"
#include  "ZepTrackp.h"
#include  "Zcode.h"
#include  "Zcnfig.h"
#include  "Zmass.h"           

       real*8 eout, cost, cs, sn, sint, Exray
       logical kbtest
       type(epTrack)::  elec1, xray
!
!           get Photo-electron energy
!       call epphotoEe(Media(MediaNo).pe,   < v8.0
       call epphotoEe(Media(MediaNo),
     *      cTrack%p%fm%p(4), eout, cost)

       if(kbtest(Eabsorb, BitPhotoElec)) then
!            energy absorbed by atom is Eabs = Eshell= Eg-(Ee-Me)
          Move%dE = cTrack%p%fm%p(4) - (eout - masele)
          Move%dEeff= Move%dE
          Move%dEioni = Move%dE
          SumDe = SumDe + Move%dE
!                  regard it as deposited in the media
!          if(Det.cmp(Cn).CountDE .ge. 1) then >>>>>>>>>>>>>>light
             call epLightPreUserde(1, cTrack)
             if( Move%Abort /= 0 ) then
                if( Move%Abort /=3 ) then
                   call epempty ! empty the stack
                   call epSkipUpdateNo
                else
                   Move%Abort=0
                endif
                  ! no flag is needed. since called from epint
                return
             endif
!          endif                               <<<<<<<<<<<<<
          Exray = 0.
       else
!             bit 1 is not on; characteristic x-ray emmission;
!             This was neglected in v8.71 or earlier.
!             we  assume 
!                1)  p.e effect takes place for the largest possible
!                    shell energy (Say, if Eg> K-shell energy, L-shell
!                    p.e effect is neglected and all p.e effect  is assumed
!                    to take for K-shell. 
!                2)  For such p.e effect, vacancy of electron level is 
!                    filled by X-ray emission ; No Auger electron emmission
!                    is considered. 1)+2) are good approximation.
         Exray = max( cTrack%p%fm%p(4) - (eout - masele), 0.d0)
       endif
!         emitted electron
       elec1 = cTrack
       call cmkptc(kelec, regptcl, -1, elec1%p)
       call kcossn(cs,sn)
       sint = sqrt(1.d0-cost**2)
       elec1%w%x = cs*sint
       elec1%w%y = sn*sint
       elec1%w%z = cost
       elec1%p%fm%p(4) = eout 
       call eptransVect(cTrack%w, elec1%w, elec1%w)
       call epe2p(elec1)
       call eppush(elec1)
!         emitted xray; assume isotropic
       if( Exray  .gt.  0.) then
          xray = cTrack
          call cmkptc(kphoton, 0, 0, xray%p)
          call episoAngle( xray%w )

!          call rndc(cost)
!          cost = 2.0*cost-1.0
!          call kcossn(cs,sn)
!          sint = sqrt(1.-cost**2)
!          xray.w.x = cs*sint
!          xray.w.y = sn*sint
!          xray.w.z = cost
          xray%p%fm%p(4) = Exray
          call eptransVect(cTrack%w, xray%w, xray%w)
          call epe2p(xray)
          call eppush(xray)
       endif
       end
!      ************
       subroutine epbrem
!      ************
       implicit none
#include  "ZepTrackv.h"
#include  "ZepTrackp.h"
#include  "Zcode.h"
#include  "Zmass.h"

!
       type(epDirec)::  w
       real*8 e1, eg, theta, cs, sn, cost, sint

       e1 = cTrack%p%fm%p(4)
!             sample brems gamma energy
       call epBrSampE(Media(MediaNo), e1, eg)

!             electron energy
       cTrack%p%fm%p(4) =  e1 - eg
       call epe2p(cTrack)
!          save electron. can assume electron dose not change angle
       call eppush(cTrack)
!         see if brem g angle needed
       if(AngleB) then
!          brems g angle relative to parent electron.
          call epBremAng(e1, masele, eg, Media(MediaNo)%Zeff, theta)

          if(theta .lt. 0.03d0) then
             sint = theta
             cost = 1.- theta**2 / 2
          else
             sint = sin(theta)
             cost = cos(theta)
          endif
          call kcossn(cs,sn)
          w%x = cs*sint
          w%y = sn*sint
          w%z = cost
          call eptransVect(cTrack%w,  w, cTrack%w)
       endif
       cTrack%p%fm%p(4) = eg
       call cmkptc(kphoton, 0, 0, cTrack%p)
       call epe2p(cTrack)       
       call eppush(cTrack)

       end

!     ************
      subroutine epanih
!     ************
      implicit none
#include  "ZepTrackv.h"

#include  "Zcode.h"
#include  "Zmass.h"

!
       type(epTrack)::   gamma
       type(epDirec)::   w
      real*8 Ee, eg1, eg2, cos1, cosr, tmp, sine, sinr
      real*8 cs, sn 
!        cpy parent info.
      gamma = cTrack
      Ee = cTrack%p%fm%p(4)
      call epanihiea(Ee, eg1, eg2, cos1, cosr)

      tmp=1.d0-cos1*cos1
      if(tmp .lt. 0.d0) then
         tmp=0.d0
         cos1=-1.d0
      endif
      sine=sqrt(tmp)
      call kcossn(cs,sn)
      w%x = cs*sine
      w%y = sn*sine
      w%z = cos1
      call eptransVect(cTrack%w, w, w)
!        save hi gamma
      call cmkptc(kphoton, 0, 0, gamma%p)
      gamma%p%fm%p(4) = eg1
#ifdef SUBSTREC
      gamma%w = w
#else
      call epsubvec( w, gamma%w)
#endif
      call epe2p(gamma)
      call eppush(gamma)
!       low gamma
      tmp=1.d0-cosr*cosr
      if(tmp .lt. 0.d0) then
         tmp=0.d0
         cosr=-1.d0
      endif
      sinr=sqrt(tmp)
      gamma%w%x = -cs*sinr
      gamma%w%y = -sn*sinr
      gamma%w%z = cosr
      call eptransVect(cTrack%w, gamma%w, gamma%w)
      gamma%p%fm%p(4) = eg2
      call epe2p(gamma)
      call eppush(gamma)
      end

!     ************
      subroutine  epknoc
      implicit none
#include  "ZepTrackp.h"
#include  "ZepTrackv.h"
#include  "Zcode.h"
#include  "Zmass.h"
           
!
      integer ic
       type(epDirec)::  w
       type(epTrack)::  survival
      real*8 Ee, e1, er, cos1, cosr, sine, cs, sn, sinr, tmp
      character*80 msg

      ic = cTrack%p%charge
      Ee = cTrack%p%fm%p(4)

      if(ic .eq. -1) then
         call epmollerea(Ee, RecoilKEmin, e1, er, cos1, cosr)
!         call epmollerea(Ee,  e1, er, cos1, cosr)  ! old
      elseif(ic .eq. 1) then
         call epbhabhae(Ee, RecoilKEmin, e1, er, cos1, cosr)
!         call epbhabhae(Ee, e1, er, cos1, cosr)  ! old
      else
         write(msg,*) ' charge =',ic,' for knocon'
         call cerrorMsg(msg, 0)
      endif

      tmp=1.d0-cos1*cos1
      if(tmp .lt. 0.d0) then
         tmp=0.d0
         cos1=1.d0
      endif
      sine=sqrt(tmp)
      call kcossn(cs,sn)
      w%x = cs*sine
      w%y = sn*sine
      w%z = cos1
      call eptransVect(cTrack%w, w, w)
      survival = cTrack
      survival%p%fm%p(4) = e1
#ifdef SUBSTREC
      survival%w = w
#else
      call epsubvec(w, survival%w)
#endif
      call cmkptc(kelec, -ic, ic, survival%p)
      call epe2p(survival)
      call eppush(survival)
!                knock on electron
      tmp=1.d0-cosr*cosr
      if(tmp .lt. 0.d0) then
         tmp=0.d0
         cosr=1.d0
      endif
      sinr = sqrt(tmp)
      survival%w%x = -cs*sinr
      survival%w%y = -sn*sinr
      survival%w%z = cosr
      survival%p%fm%p(4) = er
      call cmkptc(kelec, regptcl, -1, survival%p)
      call eptransVect(cTrack%w, survival%w, survival%w)
      call epe2p(survival)
      call eppush(survival)
      end
!     ********************************
      subroutine epsync
      implicit none
#include  "ZepTrackv.h"
#include  "Zcode.h"

       real*8 e1, eg

       e1 = cTrack%p%fm%p(4)
!             sample sync photon  energy
       call epsynce(e1, Upsilon, eg)
!             electron energy
       cTrack%p%fm%p(4) =  e1 - eg
       call cadjm(cTrack%p, cTrack%p)  ! adjust momentum due to energy change
!          save electron. can assume electron dose not change angle
       call eppush(cTrack)
       cTrack%p%fm%p(4) = eg  ! no direction change
       call cmkptc(kphoton, 0, 0, cTrack%p)
       call epe2p(cTrack)       
       call eppush(cTrack)
       end
!     ********************************
      subroutine epmpair
!         magneic pair production
      implicit none
#include  "ZepTrackv.h"
#include  "Zcode.h"

       real*8 e1, eg, chg, u

       eg = cTrack%p%fm%p(4)
!             sample pair electron of higher energy
       call epmpaire(eg, Xai, e1)
!            higher energy electron
       cTrack%p%fm%p(4) =  e1
!          save  higher energy electron.
!          can assume electron dose not change angle
       call rndc(u)
       if(u .lt. 0.5) then
          chg = -1
       else
          chg = 1
       endif
       call cmkptc(kelec, -chg, chg, cTrack%p)
       call cadjm(cTrack%p, cTrack%p)
       call eppush(cTrack)
       cTrack%p%fm%p(4) = eg - e1  
       cTrack%p%charge = chg
       cTrack%p%subcode = -chg
       call cadjm(cTrack%p, cTrack%p) 
       call epe2p(cTrack)       
       call eppush(cTrack)
       end
!
!     ************
      subroutine epNEPknoc
!     ************
      implicit none
#include  "ZepTrackv.h"
#include  "Zcode.h"
!     
       type(epDirec)::  w 
       type(epTrack)::  aTrack
      real*8  e1,  er, cos1, cosr, tmp
      real*8 cs, sn, sinr


      call epKnockea(cTrack%p, e1, er, cos1, cosr)
!
!     We can neglect angle of survival particle completely
!      tmp=1.d0-cos1*cos1
!      if(tmp .lt. 0.d0) then
!         tmp=0.d0
!         cos1=1.d0
!      endif
!      sine=sqrt(tmp)

       call kcossn(cs,sn)

!      w.x = cs*sine
!      w.y = sn*sine
!      w.z = cos1
!           
      aTrack = cTrack
      aTrack%p%fm%p(4) = e1
      call epe2p(aTrack)
      call eppush(aTrack)
!                knock on electron
      tmp=1.d0-cosr*cosr
      if(tmp .lt. 0.d0) then
         tmp=0.d0
         cosr=1.d0
      endif
      sinr = sqrt(tmp)
      w%x = -cs*sinr
      w%y = -sn*sinr
      w%z = cosr
!           
      call eptransVect(cTrack%w, w, cTrack%w)
      call cmkptc(kelec, regptcl, -1, cTrack%p)
      cTrack%p%fm%p(4) = er
      call epe2p(cTrack)
      call eppush(cTrack)
      end
!         returns dE/dx (GeV/cm2)  ; this should not be moved
!       to epquery (due to use moddedx)
      subroutine epqElossRate(dedxout)
      use moddedx
      implicit none
      real(8),intent(out):: dedxout
      dedxout = dedx
      end
!    *************************************
      subroutine epmagDefR(aTrack, mag, r)
      implicit none
!       get magnetic deflecton radius.  This is
!       approximate one.

#include  "ZepTrack.h"

       type(epTrack):: aTrack  ! input. charged particle
       type(epPos)::  mag  !   innput. magnetic field vector in 
                          !           the local coordinate
                          ! field strength is in T.
      real*8  r   ! output. Radius of magnetic defletion.  cm 
                  !         rough value.

      real*8 maxb, temp

      maxb = max (abs(mag%x), abs(mag%y), abs(mag%z))
      if(maxb .ne. 0) then
         temp = aTrack%p%fm%p(4)**2-aTrack%p%mass**2
         if(temp .le. 0.) then
            r = 1.d-4
         else
            r = 333.d0* sqrt(temp)/maxb/
     *       abs(aTrack%p%charge)
            r= max(r, 1.d-4)
         endif
      else
         r = 1.d10
      endif
      end


